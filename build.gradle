



buildscript {
	repositories {
    	mavenCentral()
	}
	dependencies {
		classpath 'org.xmlunit:xmlunit-core:2.3.0'
	}
}

import javax.xml.transform.stream.*
import javax.xml.transform.TransformerFactory
import org.xmlunit.builder.*
import org.xmlunit.diff.Diff

class TestXsltTask extends DefaultTask {

	def xslPath = "src/main/xslt/tessy-to-junit.xsl"
	def xslt = new File(xslPath).getText()
   def transformer = TransformerFactory.newInstance().newTransformer(new StreamSource(new StringReader(xslt)))

	def toXunit( String inputPath) {

		def inputFile = new File(inputPath)
		def tessy = inputFile.getText()

		def outputFile = getTemporaryDir().createTempFile("test",".xml")

		logger.info("${inputFile } ->  ${outputFile}")
		def junit = new FileOutputStream(outputFile)

		transformer.transform(new StreamSource(new StringReader(tessy)), new StreamResult(junit))
		return outputFile
	}

   @TaskAction
	def test() {
	  def output = toXunit("src/test/xslt/allpass/input.xml")

	  def expectedResult = Input.fromFile("src/test/xslt/allpass/junit-result.xml").build()

	  Diff currentDiff = DiffBuilder.compare(Input.fromFile(output))
	   		  .normalizeWhitespace()
              .withTest(expectedResult)
              .build();

     if(currentDiff.hasDifferences()) {
		  logger.error currentDiff.toString()
		  logger.error "Actual output at " + output
		  throw new GradleException("There is a difference between actual and expected xml output")
	  }
  }
}

task test(type: TestXsltTask){

}


task wrapper(type: Wrapper) {
    gradleVersion = '4.0'
}
